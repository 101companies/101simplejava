apply plugin: 'java'
apply plugin: 'eclipse'

group = 'org.softlang.company'
version = '1.0.0'

// Use the maven repository to fetch dependencies
repositories {
	mavenCentral()
}

// Implementation specifics
// This implementation uses ANTLR for Parser-Generation and JUnit for testing
dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.11+'
	compile group: 'org.antlr', name: 'antlr', version: '3.2'
}
// Use ANTLR to generate a Parser from src/main/antlr/Company.g to src/main/java/org/softlang/company/antlr
task generateFromANTLR(type: JavaExec) {
	inputs.dir file('src/main/antlr')
	outputs.dir file('src/main/java/org/softlang/company/antlr')

	classpath = compileJava.classpath

	main = 'org.antlr.Tool'
	args = ['-fo', 'src/main/java/org/softlang/company/antlr', 'src/main/antlr/Company.g']
}
// Execute ANTLR before compiling Java code
compileJava {
	dependsOn generateFromANTLR
}

// Gradle functionality
// Configure testing
test {
	testLogging {
		exceptionFormat "full"
		events "passed", "failed", "skipped"
		showStandardStreams = true
	}
}
// Prepare for eclipse
eclipse.classpath.file {
	// Beautification
	whenMerged {
		classpath ->
			classpath.entries.removeAll {
				entry -> ( entry.kind == 'src' && entry.path == '' )
			}
	}
	// Gather dependencies
	withXml {
		def root = it.asElement()
		def owner = root.getOwnerDocument()
		def newNode = owner.createElement('classpathentry')
		newNode.setAttribute('kind', 'src')
		newNode.setAttribute('path', 'src/main/antlr')
		root.appendChild(newNode)
		for ( child in root.getChildNodes() ) {
			if ( child.getAttributes() != null ) {
				if ( child.getAttributes().getNamedItem('kind') != null ) {
					if ( child.getAttributes().getNamedItem('kind').getTextContent() == 'lib' ) {
						if ( child.getAttributes().getNamedItem('sourcepath') != null ) {
							def oldPath = child.getAttributes().getNamedItem('path').getTextContent()
							def newPath = 'libs/' + new File(oldPath).getName()
							copy {
								from oldPath
								into 'libs/'
							}
							def oldSourcePath = child.getAttributes().getNamedItem('sourcepath').getTextContent()
							def newSourcePath = 'libs/' + new File(oldSourcePath).getName()
							copy {
								from oldSourcePath
								into 'libs/'
							}
							child.setAttribute('path', newPath)
							child.setAttribute('sourcepath', newSourcePath)
							child.removeAttribute('exported')
							println 'Moving library from ' + oldPath + ' with'
							println '       sources ' + oldSourcePath
							println '       to libs/.'
						}
						else {
							def oldPath = child.getAttributes().getNamedItem('path').getTextContent()
							def newPath = 'libs/' + new File(oldPath).getName()
							copy {
								from oldPath
								into 'libs/'
							}
							child.setAttribute('path', newPath)
							println 'Moving library from ' + oldPath
							println '       to libs/.'
						}
					}
					else {
						// non-lib kind
					}
				}
			}
		}
	}
}
// Additional cleanup for eclipse
cleanEclipse {
	doFirst {
		delete '.settings', 'bin', 'libs', 'outputs'
	}
}
// Additional cleanup for ANTLR
clean {
	dependsOn cleanEclipse
	doFirst {
		delete 'src/main/java/org/softlang/company/antlr'
	}
}