apply plugin: 'java'
apply plugin: 'eclipse'

group = 'org.softlang.company'
version = '1.0.0'

// Use the maven repository to fetch dependencies
repositories {
	mavenCentral()
}
// This implementation uses JUnit for testing
dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.11+'
}
// Configure testing
test {
	testLogging {
		exceptionFormat "full"
		events "passed", "failed", "skipped"
		showStandardStreams = true
	}
}
// Beautify eclipse
eclipse.classpath.file {
	whenMerged {
		classpath -> classpath.entries.removeAll {
			entry -> entry.kind == 'src' && entry.path == ''
		}
	}
}
// Additional cleanup for eclipse
cleanEclipse {
	doFirst {
		delete '.settings', 'bin'
	}
}
// Additional cleanup for outputs and generated files
clean {
	dependsOn cleanEclipse
	doFirst {
		delete 'outputs'
		delete 'src/main/java/org/softlang/company/model'
	}
}
// Generate model classes with xjc
task xjc(type: Exec) {
	commandLine 'xjc'
	args = ['inputs/Company.xsd', '-d', 'src/main/java']
	doFirst {
		mkdir 'src/main/java/org/softlang/company/model'
	}
}
// Hook xjc
compileJava {
	dependsOn 'xjc'
}